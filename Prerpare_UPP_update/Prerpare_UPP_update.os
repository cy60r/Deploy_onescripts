#Использовать Tmail
#Использовать ReadParams
#Использовать sql
#Использовать gitrunner
#Использовать xml-parser
#Использовать tempfiles
#Использовать 1commands
#Использовать Ext_v8runner
#Использовать Ext_v8storage
#Использовать Ext_v8rac
#Использовать Ext_TLog

Перем Параметры; 			// Параметры из конфигурационного файла json
Перем Логирование; 			// История лога выполнения
Перем УправлениеКластером; 	// Подключение к кластеру
Перем Конфигуратор; 		// Подключение к конфигуратору
Перем ГитРепозиторий; 		// Гит репозиторий
Перем ИнтернетПочта;		// Учетная запись электронной почты для отправки писем

#Область НачальныеНастройки

Процедура ПрочитатьПараметрыИзФайла()
	
	УстановитьТекущийКаталог("C:\scripts\Prerpare_UPP_update");
	Параметры = ЧтениеПараметров.Прочитать(ОбъединитьПути(ТекущийКаталог(), "Prerpare_UPP_update.json"));
	
КонецПроцедуры

Процедура ИнициализироватьЛог()
	
	Логирование = Новый ТУправлениеЛогированием();
	Логирование.ДатаВремяВКаждойСтроке = Истина;
	Логирование.ВыводитьСообщенияПриЗаписи = Истина;
	Логирование.СоздатьФайлЛога(Параметры["ИмяЛога"], Параметры["КаталогЛога"]);
	Логирование.ЗаписатьСтрокуЛога("Начало подготовки копии");
	Логирование.УвеличитьУровень();
	
КонецПроцедуры

Процедура ИнициализироватьПочту()

	ИнтернетПочта = Новый ТУправлениеЭлектроннойПочтой();

	УчетнаяЗаписьЭП = ИнтернетПочта.УчетнаяЗаписьЭП;
	УчетнаяЗаписьЭП.АдресSMTP 			= Параметры["АдресSMTP"];
	УчетнаяЗаписьЭП.ПортSMTP 			= 25;
	Логирование.ЗаписатьСтрокуЛога("Электронная почта подключена");

КонецПроцедуры

Процедура УстановитьКонтекстКонфигурации()
	
	Конфигуратор = Новый УправлениеКонфигуратором();
	Конфигуратор.УстановитьКонтекст("/S" + Параметры["ИмяСервера"] + "\" + Параметры["ИмяБазы"],
		Параметры["ПользовательИБ"], Параметры["ПарольИБ"]);
	Логирование.ЗаписатьСтрокуЛога("Информационная база подключена");
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСКластером

Процедура ПодключитьУправлениеКластером()
	
	УправлениеКластером = Новый УправлениеКластером;
	УправлениеКластером.УстановитьКластер(Параметры["ИмяСервераКластера"]);
	УправлениеКластером.ИспользоватьВерсию(Параметры["ВерсияПлатформы"]);
	УправлениеКластером.Подключить();
	Логирование.ЗаписатьСтрокуЛога("Управление кластером подключено");

КонецПроцедуры

Процедура ОтключитьПользователейИБ()

	УправлениеКластером.ОтключитьСеансыИнформационнойБазы(Параметры["ИмяБазы"]);
	Логирование.ЗаписатьСтрокуЛога("Сеансы отключены");

КонецПроцедуры

Процедура ОтключитьСоединенияИнформационнойБазы()

	УправлениеКластером.ОтключитьСоединенияИнформационнойБазы(Параметры["ИмяБазы"]);
	Логирование.ЗаписатьСтрокуЛога("Соединения отключены");	

КонецПроцедуры

#КонецОбласти

#Область РаботаСХранилищем

Процедура ОтключитьХранилище(Отказ)
	
	Если Не Отказ Тогда
		
		Конфигуратор.ОтключитьсяОтХранилища();
		Логирование.ЗаписатьСтрокуЛога("Хранилище отключено");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодключитьХранилище(Отказ, ПутьКХранилищу)
	
	Если Не Отказ Тогда
		
		Конфигуратор.ПодключитьсяКХранилищу(ПутьКХранилищу,
			Параметры["ПользовательХранилища"], Параметры["ПарольХранилища"], Истина, Истина);
		Логирование.ЗаписатьСтрокуЛога("Хранилище " + ПутьКХранилищу + " подключено");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаССУБД

Процедура ОчиститьТаблицы(Отказ)
	
	Если Не Отказ Тогда
		
		Соединение = Новый Соединение();
		Соединение.ТипСУБД 			= Соединение.ТипыСУБД.MSSQLServer;
		Соединение.Сервер 			= Параметры["ИмяСервераSQL"];
		Соединение.ИмяБазы 			= Параметры["ИмяБазыSQL"];
		Соединение.ИмяПользователя 	= Параметры["ИмяПользователяSQL"];
		Соединение.Пароль 			= Параметры["ПарольSQL"];
		Если Соединение.Открыть() Тогда
		
			Для каждого Таблица Из Параметры["ТаблицыДляОчистки"] Цикл
			
				Запрос = Новый Запрос();
				Запрос.УстановитьСоединение(Соединение);
				Запрос.Текст = "TRUNCATE TABLE " + Таблица + ";";
				Запрос.ВыполнитьКоманду();	

			КонецЦикла;
		
			Логирование.ЗаписатьСтрокуЛога("Небходимые таблицы очищены");

		Иначе
			
			Отказ = Истина;
			Логирование.ЗаписатьСтрокуЛога("Не удалось подключиться к SQL");

		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область РаботасКонфигураций

Процедура ОбновитьКонфигурацию(Отказ)
	
	Если Не Отказ Тогда

		Конфигуратор.ОбновитьКонфигурациюБазыДанныхНаСервере();
		Логирование.ЗаписатьСтрокуЛога("Конфигурация обновлена");

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ДымовыеТесты

Процедура ВыполнитьДымовыеТесты()

	ИмяФайлаОтчета = ИмяФайлаОтчетаТестирования();

	Команда = Новый Команда;
	ПараметрыВхода 		= СтрШаблон("/IBConnectionString Srvr=%1;Ref=%2 /N %3 /P %4", 
	Параметры["ИмяСервера"], Параметры["ИмяБазы"], Параметры["ПользовательИБ"], Параметры["ПарольИБ"]);
	ПараметрОбработки 	= СтрШаблон("/Execute %1 /C """, Команда.ОбернутьВКавычки(Параметры["ОбработкаЗапускаТестов"]));
	ПараметрКонфига 	= СтрШаблон("xddConfig %1;", ДопПараметрОбработки(Параметры["ФайлКонфигурацииТестов"]));
	ПараметрКаталога 	= СтрШаблон("xddRun ЗагрузчикКаталога %1;", ДопПараметрОбработки(Параметры["КаталогТестов"]));
	ПараметрОтчета 		= СтрШаблон("xddReport ГенераторОтчетаMXL %1;", ДопПараметрОбработки(ИмяФайлаОтчета));
	ПараметрЗакрытия 	= СтрШаблон("%1;""", Параметры["ПараметрЗакрытия"]);
	
	Команда.УстановитьКоманду(Параметры["ПутьДляЗапуска1С"]);
	Команда.ДобавитьПараметр(Параметры["ПараметрПриложения"]);
	Команда.ДобавитьПараметр(ПараметрыВхода);
	Команда.ДобавитьПараметр(Параметры["ПараметрыЗапуска"]);
	Команда.ДобавитьПараметр(ПараметрОбработки);
	Команда.ДобавитьПараметр(ПараметрКонфига);
	Команда.ДобавитьПараметр(ПараметрКаталога);
	Команда.ДобавитьПараметр(ПараметрОтчета);
	Команда.ДобавитьПараметр(ПараметрЗакрытия);
	
	КодВозврата = Команда.Исполнить();
	Если КодВозврата = 0 Тогда
	
		Логирование.ЗаписатьСтрокуЛога("Дымовые тесты выполнены");
		ТекстСообщения = ТекстСообщенияФайла(ИмяФайлаОтчета);
		Если ЗначениеЗаполнено(ТекстСообщения) Тогда
		
			ОтправитьОтчетТестирования(ТекстСообщения, ИмяФайлаОтчета);	

		Иначе

			УдалитьФайлы(ИмяФайлаОтчета);

		КонецЕсли;
	
	Иначе
	
		Логирование.ЗаписатьСтрокуЛога("Ошибка выполнение дымовых тестов");
	
	КонецЕсли;	
	
КонецПроцедуры

Функция ИмяФайлаОтчетаТестирования()
	
	НаименованиеФайла = Формат(ТекущаяДата(), "ДФ=dd_MM_yyyy_ЧЧ_мм_сс") + "_smoke_report.txt";
	ИмяФайла = ОбъединитьПути(Параметры["КаталогЛоговТестов"], НаименованиеФайла);
	НовыйТекстовыйФайл = Новый ТекстовыйДокумент();
	НовыйТекстовыйФайл.Записать(ИмяФайла);
	Возврат ИмяФайла;

КонецФункции

Функция ТекстСообщенияФайла(ИмяФайла)

	ТекстовыйФайл = Новый ЧтениеТекста(ИмяФайла);
	Сообщение = ТекстовыйФайл.ПрочитатьСтроку();
	ТекстовыйФайл.Закрыть();
	
	Возврат Сообщение;
	
КонецФункции

Функция ДопПараметрОбработки(Знач Строка) Экспорт

	Пока СтрНайти(Строка, "/") <> 0 Цикл
	
		Строка = СтрЗаменить(Строка, "/", "\");

	КонецЦикла;

	Возврат """""" + Строка + """""";

КонецФункции

#КонецОбласти

#Область ИнтернетПочта

Процедура ОтправитьОтчетТестирования(ТекстСообщения, ИмяФайлаОтчета)
	
	СтруктураСообщения = ИнтернетПочта.СтруктураСообщения;
	СтруктураСообщения.АдресЭлектроннойПочтыПолучателя = Параметры["АдресПолучателя"];
		
	СтруктураСообщения = ИнтернетПочта.СтруктураСообщения;
	СтруктураСообщения.АдресЭлектроннойПочтыОтправителя = Параметры["АдресОтправителя"];
	СтруктураСообщения.ТемаСообщения = Параметры["ТемаПисьмаТесты"];
	
	СтруктураСообщения.ТипТекстаПочтовогоСообщения = "Строка";
	СтруктураСообщения.Вставить("ТекстСообщения", ТекстСообщения);
	
	СтруктураСообщения.Вложения = ИмяФайлаОтчета;

	Если ИнтернетПочта.ОтправитьСообщение() Тогда
			
		Логирование.ЗаписатьСтрокуЛога("Ошибки дымовых тестов отправлены");
	
	Иначе
		
		Логирование.УвеличитьУровень();
		Логирование.ЗаписатьСтрокуЛога(ОписаниеОшибки());
		Логирование.УменьшитьУровень();
	
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ЗаключительныеОперации

Процедура ЗавершитьВыполнение()
	
	Логирование.УменьшитьУровень();
	Логирование.ЗаписатьСтрокуЛога("Копия подготовлена");
	
КонецПроцедуры

Процедура ОбработатьИсключение(ТекстОшибки)
	
	Логирование.УвеличитьУровень();
	Логирование.ЗаписатьСтрокуЛога(ТекстОшибки);
	Логирование.УменьшитьУровень();
	Логирование.УменьшитьУровень();
	Логирование.ЗаписатьСтрокуЛога("Во время подготовки произошла ошибка!");
	
КонецПроцедуры

#КонецОбласти

ПрочитатьПараметрыИзФайла();
ИнициализироватьЛог();
ИнициализироватьПочту();
Отказ = Ложь;

Попытка
	
	ПодключитьУправлениеКластером();
	ОтключитьПользователейИБ();
	ОтключитьСоединенияИнформационнойБазы();

	УстановитьКонтекстКонфигурации();
	
	// Подключаем хранилище UPP_work
	ОтключитьХранилище(Отказ);
	ПодключитьХранилище(Отказ, Параметры["РабочееХранилище"]);
	ОбновитьКонфигурацию(Отказ);

	// // Запуск дымовых тестов
	ВыполнитьДымовыеТесты();
		
	// Подключаем хранилище UPP
	ОтключитьХранилище(Отказ);
	ПодключитьХранилище(Отказ, Параметры["ХранилищеРазработки"]);
		
	// Подготавливаем и обновляем конфигурацию
	ОчиститьТаблицы(Отказ);
	ОбновитьКонфигурацию(Отказ);
	
	ЗавершитьВыполнение();
	
Исключение
	
	ОбработатьИсключение(ОписаниеОшибки());
	
КонецПопытки;