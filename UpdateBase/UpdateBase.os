#Использовать ReadParams
#Использовать InternetMail
#Использовать cmdline
#Использовать Ext_v8rac
#Использовать Ext_v8runner
#Использовать Ext_Tlog

Перем УправлениеКластером; 			// Подключение к кластеру
Перем Конфигуратор; 				// Подключение к конфигуратору
Перем Параметры; 					// Параметры из конфигурационного файла json
Перем Логирование; 					// История лога выполнения
Перем ПараметрыАвторизацииИБ; 		// Параметры для авторизации на кластере
Перем ДатаНачалаБлокировки; 		// Дата начала блокировки сеансов
Перем ИнтернетПочта;				// Учетная запись электронной почты для отправки писем

Процедура ПрочитатьПараметрыИзФайла()

	УстановитьТекущийКаталог("C:\scripts\UpdateBase");
	Параметры = ЧтениеПараметров.Прочитать(ОбъединитьПути(ТекущийКаталог(), "UpdateBase.json"));

КонецПроцедуры

Процедура ПолучитьАргументы()

	Парсер = Новый ПарсерАргументовКоманднойСтроки();

	Парсер.ДобавитьИменованныйПараметр("--denied-from");
	
	ПараметрыКоманднойСтроки 	= Парсер.Разобрать(АргументыКоманднойСтроки);
	ДатаНачалаБлокировки 		= ПараметрыКоманднойСтроки["--denied-from"];

КонецПроцедуры

Процедура ИнициализироватьЛог()
	
	Логирование = Новый ТУправлениеЛогированием();
	Логирование.ДатаВремяВКаждойСтроке = Истина;
	Логирование.ВыводитьСообщенияПриЗаписи = Истина; 
	Логирование.СоздатьФайлЛога(Параметры["ИмяЛога"], Параметры["КаталогЛога"]);
	Логирование.ЗаписатьСтрокуЛога("Начало обновления базы " + Параметры["ИмяБазы"]);
	Логирование.УвеличитьУровень();

КонецПроцедуры

Процедура ИнициализироватьПочту()
	
	ПрофильПочты = Новый ИнтернетПочтовыйПрофиль;

	ПрофильПочты.АдресСервераSMTP    = Параметры["АдресSMTP"];
	ПрофильПочты.ПользовательSMTP    = Параметры["ПользовательSMTP"];
	ПрофильПочты.ПарольSMTP          = Параметры["ПарольSMTP"];
	ПрофильПочты.ПортSMTP 			= Параметры["ПортSMTP"];
	ПрофильПочты.ИспользоватьSSLSMTP = Ложь;
	ПрофильПочты.ТолькоЗащищеннаяАутентификацияSMTP = Ложь;

	ИнтернетПочта = Новый ИнтернетПочта;
	ИнтернетПочта.Подключиться(ПрофильПочты);
	Логирование.ЗаписатьСтрокуЛога("Профиль почты подключен");

КонецПроцедуры

Процедура ПодключитьУправлениеКластером()
	
	УправлениеКластером = Новый УправлениеКластером;
	УправлениеКластером.УстановитьКластер(Параметры["ИмяСервераКластера"]);
	УправлениеКластером.ИспользоватьВерсию(Параметры["ВерсияПлатформы"]);
	УправлениеКластером.УстановитьАвторизациюКластера(Параметры["ПользовательКластера"], Параметры["ПарольКластера"]);
	УправлениеКластером.Подключить();
	УправлениеКластером.УстановитьАвторизациюИнформационнойБазы(Параметры["ИмяБазы"], 
	Параметры["ПользовательИБ"], Параметры["ПарольИБ"]);
	ПараметрыАвторизацииИБ = ПараметрыАвторизацииИБ();
	Логирование.ЗаписатьСтрокуЛога("Управление кластером подключено");

КонецПроцедуры

Функция БлокировкаУстановлена()
	
	ОписаниеИБ = УправлениеКластером.ПолучитьПодробноеОписаниеИнформационнойБазы(Параметры["ИмяБазы"]);
	Результат = ОписаниеИБ.ЗапретПодключенияСессий;
	Логирование.ЗаписатьСтрокуЛога("ИБ " + ?(Результат, "", "не ") + "требует обновления");

	Возврат Результат; 

КонецФункции

Функция ПараметрыАвторизацииИБ()
	
	ПараметрыАвторизации = Новый Структура();
	ПараметрыАвторизации.Вставить("Пользователь", Параметры["ПользовательИБ"]);
	ПараметрыАвторизации.Вставить("Пароль", Параметры["ПарольИБ"]);

	Возврат ПараметрыАвторизации;

КонецФункции

Процедура ОтключитьПользователейИБ()

	УправлениеКластером.ОтключитьСеансыИнформационнойБазы(Параметры["ИмяБазы"]);
	Логирование.ЗаписатьСтрокуЛога("Сеансы отключены");

КонецПроцедуры

Процедура ОтключитьСоединенияИнформационнойБазы()

	УправлениеКластером.ОтключитьСоединенияИнформационнойБазы(Параметры["ИмяБазы"]);
	Логирование.ЗаписатьСтрокуЛога("Соединения отключены");	

КонецПроцедуры

Процедура УстановитьКонтекстКонфигурации()

	Конфигуратор = Новый УправлениеКонфигуратором();
	Конфигуратор.УстановитьКонтекст("/S" + Параметры["ИмяСервера"] + "\" + Параметры["ИмяБазы"], 
	Параметры["ПользовательИБ"], Параметры["ПарольИБ"]);
	Логирование.ЗаписатьСтрокуЛога("Информационная база подключена");

КонецПроцедуры

Процедура ПолучитьКонфигурациюХранилища()

	Конфигуратор.УстановитьКлючРазрешенияЗапуска(Параметры["КлючРазрешенияЗапуска"]);
	Конфигуратор.ОбновитьКонфигурациюБазыДанныхИзХранилища(Параметры["ПутьКХранилищу"], 
	Параметры["ПользовательХранилища"], Параметры["ПарольХранилища"]);
	Логирование.ЗаписатьСтрокуЛога("Информационная база обновлена");

КонецПроцедуры

Процедура ОтправитьПисьмоЗавершении()
	
	Сообщение = Новый ИнтернетПочтовоеСообщение;
	Сообщение.Получатели.Добавить(Параметры["АдресПолучателя"]);
	Сообщение.Отправитель = Параметры["АдресОтправителя"];
	Сообщение.Тема        = СтрШаблон(Параметры["ТемаПисьма"], ДатаНачалаБлокировки);
	ТекстПисьма = ТекстСообщенияОбУспешномОбновлении();

	Сообщение.Тексты.Добавить(ТекстПисьма, ТипТекстаПочтовогоСообщения.ПростойТекст);
	
	Попытка
		
		ИнтернетПочта.Послать(Сообщение, ОбработкаТекстаИнтернетПочтовогоСообщения.НеОбрабатывать, ПротоколИнтернетПочты.SMTP);
		Логирование.ЗаписатьСтрокуЛога("Письмо о завершении обновления отправлено");

	Исключение

		Логирование.УвеличитьУровень();
		Логирование.ЗаписатьСтрокуЛога(ОписаниеОшибки());
		Логирование.УменьшитьУровень();

	КонецПопытки;

КонецПроцедуры

Функция ТекстСообщенияОбУспешномОбновлении()

	ТекстовыйФайл = Новый ЧтениеТекста(ОбъединитьПути(ТекущийКаталог(), "UpdateBaseMessage.txt"));
	Сообщение = ТекстовыйФайл.Прочитать();
	ТекстовыйФайл.Закрыть();
	
	Возврат Сообщение;

КонецФункции

Процедура СнятьБлокировкуИБ()

	УправлениеКластером.СнятьБлокировкуИнформационнойБазы(Параметры["ИмяБазы"], Ложь);
	Логирование.ЗаписатьСтрокуЛога("Блокировка ИБ снята");	

КонецПроцедуры

Процедура ЗавершитьОбновление()

	Логирование.УменьшитьУровень();
	Логирование.ЗаписатьСтрокуЛога("Окончание обновления базы");	

КонецПроцедуры

Процедура ОбработатьИсключение(ТекстОшибки)

	СнятьБлокировкуИБ();
	Логирование.ЗаписатьСтрокуЛога(ТекстОшибки);
	ОтправитьПисьмоОбОшибке(ТекстОшибки);
	Логирование.УменьшитьУровень();
	Логирование.ЗаписатьСтрокуЛога("Во время обновления произошла ошибка!");	

КонецПроцедуры

Процедура ОтправитьПисьмоОбОшибке(ТекстОшибки)
	
	Сообщение = Новый ИнтернетПочтовоеСообщение;
	Сообщение.Получатели.Добавить(Параметры["АдресПолучателяОшибки"]);
	Сообщение.Отправитель = Параметры["АдресОтправителя"];
	Сообщение.Тема        = "ОШИБКА " + СтрШаблон(Параметры["ТемаПисьма"], ДатаНачалаБлокировки);
	Сообщение.Тексты.Добавить(ТекстОшибки, ТипТекстаПочтовогоСообщения.ПростойТекст);
	
	Попытка
		
		ИнтернетПочта.Послать(Сообщение, ОбработкаТекстаИнтернетПочтовогоСообщения.НеОбрабатывать, ПротоколИнтернетПочты.SMTP);
		Логирование.ЗаписатьСтрокуЛога("Письмо об ошибке отправлено");

	Исключение

		Логирование.УвеличитьУровень();
		Логирование.ЗаписатьСтрокуЛога(ОписаниеОшибки());
		Логирование.УменьшитьУровень();

	КонецПопытки;

КонецПроцедуры

Попытка

	ПолучитьАргументы();
	ПрочитатьПараметрыИзФайла();
	ИнициализироватьЛог();
	ИнициализироватьПочту();
	ПодключитьУправлениеКластером();

	Если БлокировкаУстановлена() Тогда

		ОтключитьПользователейИБ();
		ОтключитьСоединенияИнформационнойБазы();

		УстановитьКонтекстКонфигурации();
		ПолучитьКонфигурациюХранилища();
		
		СнятьБлокировкуИБ();
		ОтправитьПисьмоЗавершении();

	КонецЕсли;

	ЗавершитьОбновление();

Исключение

	ОбработатьИсключение(ОписаниеОшибки());

КонецПопытки;